apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

allprojects {
    repositories {
        mavenLocal()
    }
}

configurations {
    all*.exclude group: 'com.google.android', module: 'android'
    all*.exclude group: 'com.android.support', module: 'support-media-compat'
    all*.exclude group: 'com.github.tony19', module: 'apktool-lib'
}

dependencies {
    implementation project(':integration-android')
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:support-compat:27.1.1'
    implementation 'com.android.support:support-fragment:27.1.1'
    implementation 'com.android.support:support-core-utils:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:multidex:1.0.2'

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'


    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:runtime:1.1.1"

    // optional - Test helpers for LiveData
    testImplementation "android.arch.core:core-testing:1.1.1"


    implementation "android.arch.persistence:db:1.1.1"
    implementation "android.arch.persistence.room:runtime:1.1.1"
    annotationProcessor "android.arch.persistence.room:compiler:1.1.1"
    implementation 'org.bitcoinj:onixcoinj-core:0.14.7.2'
    implementation 'com.google.protobuf:protobuf-java:2.6.1'
    implementation 'com.google.guava:guava:24.1-android'
    implementation 'com.google.zxing:core:3.3.3'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.squareup.moshi:moshi:1.6.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.github.tony19:logback-android:1.1.1-8'
    implementation 'com.google.code.findbugs:jsr305:2.0.3'
    testImplementation 'junit:junit:4.12'
}

ext {
    archivesBaseName = 'onixcoin-wallet'
}

android {
    compileSdkVersion 'android-27'
    buildToolsVersion '27.0.3'
    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        //multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        applicationId 'de.schildbach.wallet_onix'
        generatedDensities = ['hdpi', 'xhdpi']

        dexOptions {
            preDexLibraries false
        }

        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }

        externalNativeBuild {
            cmake {
                cFlags '-DHAVE_CONFIG_H'
            }
        }
    }

    buildTypes {
        all {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
        debug {
            debuggable false
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
    }

    externalNativeBuild {
        cmake {
            path 'cpp/CMakeLists.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/*.version'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'okhttp3/internal/publicsuffix/publicsuffixes.gz'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
    }
}

task svgToPngMipmap(type: fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask) {
    from = files('graphics/mipmap')
    to = file('res')
    targetedDensities = ['hdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'mipmap'
}
